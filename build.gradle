plugins {
    id 'java'
    id 'io.typecraft.gradlesource.spigot' version '1.0.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.sasuked'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'

    implementation "net.kyori:adventure-api:4.12.0"
    implementation "net.kyori:adventure-platform-bukkit:4.2.0"

    compileOnly 'me.clip:placeholderapi:2.11.2'
}

shadowJar {
    relocate( 'net.kyori', 'io.github.sasuked.chatplugin.kyori' )
}

spigotRemap {
    spigotVersion.set("1.17.1")
    sourceJarTask.set(tasks.shadowJar) // or `tasks.shadowJar` if you use Shadow plugin.
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
